plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id "com.google.protobuf" version "0.9.4"
    id "org.sonarqube" version "4.4.1.3373"
}

sonar {
    properties {
        property "sonar.projectKey", "xd6d_bookstore"
        property "sonar.organization", "xd6d"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    grpcVersion = '1.62.2'
    mapstructVersion = '1.5.5.Final'
    citrusVersion = '4.2.0'
    grpcSpringVersion = '3.0.0.RELEASE'
    reactiveGrpcVersion = '1.2.4'
}

dependencies {
    implementation "net.devh:grpc-server-spring-boot-starter:${grpcSpringVersion}"
    testImplementation "net.devh:grpc-client-spring-boot-starter:${grpcSpringVersion}"
    implementation "com.salesforce.servicelibs:reactor-grpc-stub:${reactiveGrpcVersion}"
    testImplementation 'io.projectreactor:reactor-test:3.6.0'
    implementation 'org.springframework.boot:spring-boot-starter-webflux:3.2.4'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'//
    implementation 'org.postgresql:postgresql'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
    runtimeOnly "io.grpc:grpc-netty-shaded:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    testImplementation "io.grpc:grpc-testing:$grpcVersion"
    testImplementation group: 'org.citrusframework', name: 'citrus-base', version: "$citrusVersion"
    testImplementation group: 'org.citrusframework', name: 'citrus-spring', version: "$citrusVersion"
    testImplementation "org.citrusframework:citrus-sql:${citrusVersion}"
    testImplementation "org.citrusframework:citrus-junit5:$citrusVersion"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.0.0"
    }
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:${reactiveGrpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            reactor {}
        }
    }
}

sourceSets {
    main {
        proto {
            srcDir "../proto"
        }
    }
    test {
        proto {
            srcDir "../proto"
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
